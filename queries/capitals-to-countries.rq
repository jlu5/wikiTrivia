# P31 = instance of, Q3624078 = sovereign state
# P1376 = capital of
# P582 = end time, P585 = point in time (filter out former capitals)
# NOTE: in the SPARQL client, we need to add a language specifically as [AUTO_LANGUAGE] won't work
SELECT DISTINCT ?city ?cityLabel ?country ?countryLabel ?countryAltLabel WHERE {
  ?country wdt:P31 wd:Q3624078.
  ?city p:P1376 ?capitalStatement.
  ?capitalStatement ps:P1376 ?country.
  # Exclude former capitals (capital relations with an end time)
  FILTER(NOT EXISTS { ?capitalStatement (pq:P582|pq:P585) ?endTime. })
  # Exclude former capitals (cities with former capitals tag)
  FILTER(NOT EXISTS { ?city wdt:P31 wd:Q27554677. })
  # Exclude historical countries
  FILTER(NOT EXISTS { ?country wdt:P31 wd:Q3024240. })
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
